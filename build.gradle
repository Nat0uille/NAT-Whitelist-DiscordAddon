plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'fr.Nat0uille'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")

    // Database drivers
    implementation 'com.mysql:mysql-connector-j:9.5.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.7'

    // Connection pooling
    implementation 'com.zaxxer:HikariCP:7.0.2'

    // Discord Bot
    implementation("net.dv8tion:JDA:6.3.0")
}



def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    // Exclure les signatures de modules pour Ã©viter les conflits
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    relocate 'net.dv8tion.jda', 'fr.nat0uille.libs.jda'
}

build {
    dependsOn shadowJar
}

